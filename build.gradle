apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'war'

group = 'com.qinglan.sdk.server'
version = '1.0.0'

description = """release-sdk-service"""

sourceCompatibility = JavaVersion.VERSION_1_7
targetCompatibility = JavaVersion.VERSION_1_7
//apply from: 'gretty.plugin'
tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

repositories {

    maven { url "http://repo.maven.apache.org/maven2" }
}
dependencies {
    compile fileTree(dir: 'libs', include: "*.jar")
    compile group: 'com.aliyun', name: 'aliyun-java-sdk-core', version: '3.2.8'
    compile group: 'com.aliyun', name: 'aliyun-java-sdk-dysmsapi', version: '1.1.0'

    compile group: 'org.springframework', name: 'spring-webmvc', version: "${springframework_version}"
    compile group: 'org.springframework', name: 'spring-jdbc', version: "${springframework_version}"
    compile group: 'org.springframework.data', name: 'spring-data-redis', version: "${spring_data_redis_version}"
    compile group: 'redis.clients', name: 'jedis', version: "${jedis_version}"

    compile group: 'org.mybatis', name: 'mybatis-spring', version: "${mybatis_spring_version}"
    compile group: 'org.mybatis', name: 'mybatis', version: "${mybatis_version}"

    compile group: 'com.lmax', name: 'disruptor', version: '3.2.1'
    compile group: 'org.aspectj', name: 'aspectjrt', version: '1.7.3'
    compile group: 'org.aspectj', name: 'aspectjweaver', version: '1.7.3'

    compile group: 'com.cloudhopper.proxool', name: 'proxool', version: '0.9.1'
    compile group: 'com.cloudhopper.proxool', name: 'proxool-cglib', version: '0.9.1'

    compile group: 'mysql', name: 'mysql-connector-java', version: '5.1.33'

    compile group: 'org.apache.httpcomponents', name: 'httpclient', version: '4.3'
    compile group: 'org.codehaus.jackson', name: 'jackson-mapper-asl', version: '1.9.11'

    compile group: 'commons-io', name: 'commons-io', version: '2.4'
    compile group: 'org.apache.commons', name: 'commons-lang3', version: '3.2.1'
    compile group: 'commons-httpclient', name: 'commons-httpclient', version: '3.0.1'
    compile group: 'jstl', name: 'jstl', version: '1.2'
    compile(group: 'org.apache.kafka', name: 'kafka_2.9.2', version: '0.8.1.1') {
        exclude(module: 'zookeeper')
        exclude(module: 'log4j')
    }
    compile group: 'org.projectlombok', name: 'lombok', version: '1.16.8'
    compileOnly group: 'javax.servlet', name: 'servlet-api', version: '2.4'

    testCompile group: 'org.springframework', name: 'spring-test', version: '3.2.3.RELEASE'
    testCompile group: 'junit', name: 'junit', version: '4.8.1'
}

sourceSets {
    main {
        java {
            srcDirs = ['src/main/java']
        }
        resources {
            srcDirs = ['src/main/resources'/*, 'src/main/config/prod'*/, 'src/main/config/dev']
        }
    }
}

configurations {
    mybatisGenerator
}

dependencies {
    mybatisGenerator 'org.mybatis.generator:mybatis-generator-core:1.3.5'
    mybatisGenerator("mysql:mysql-connector-java:${mysql_connector_version}")
    mybatisGenerator 'tk.mybatis:mapper:3.3.9'
}

def getDbProperties = {
    def properties = new Properties()
    file("${res_path}/application.properties", "${res_path}/generator/generator.properties").withInputStream { inputStream ->
        properties.load(inputStream)
    }
    properties
}

task mybatisGenerate << {
    def properties = getDbProperties()
    ant.properties['targetProject'] = projectDir.path
    ant.properties['driverClass'] = properties.getProperty("jdbc.driver")
    ant.properties['connectionURL'] = properties.getProperty("jdbc.url")
    ant.properties['userId'] = properties.getProperty("jdbc.username")
    ant.properties['password'] = properties.getProperty("jdbc.password")
    ant.properties['src_main_java'] = sourceSets.main.java.srcDirs[0].path
    ant.properties['src_main_resources'] = sourceSets.main.resources.srcDirs[0].path
    ant.properties['modelPackage'] = properties.getProperty("package.model")
//    ant.properties['mapperPackage'] = properties.getProperty("package.mapper")
    ant.properties['sqlMapperPackage'] = properties.getProperty("package.xml")
    ant.taskdef(
            name: 'mbgenerator',
            classname: 'org.mybatis.generator.ant.GeneratorAntTask',
            classpath: configurations.mybatisGenerator.asPath
    )
    ant.mbgenerator(overwrite: true,
            configfile: properties.getProperty("config.path"), verbose: true) {
        propertyset {
            propertyref(name: 'targetProject')
            propertyref(name: 'userId')
            propertyref(name: 'driverClass')
            propertyref(name: 'connectionURL')
            propertyref(name: 'password')
            propertyref(name: 'src_main_java')
            propertyref(name: 'src_main_resources')
            propertyref(name: 'modelPackage')
//            propertyref(name: 'mapperPackage')
            propertyref(name: 'sqlMapperPackage')
        }
    }
}

//gretty {
//    contextPath = '/'
//    jvmArgs = ["-Dcfg.env=local"]
//    servletContainer = 'tomcat8'
//    httpPort = 8081
//    // ... many more properties
//}


